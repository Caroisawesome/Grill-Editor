<html>
  <head>
    <title>CoffeeScript & Canvas ABM Test</title>
    <script src="agentscript.js"></script>
    <script src="coffee-script.js"></script>
    <script type="text/coffeescript">
    class MyModel extends ABM.Model
      u = ABM.util # alias for ABM.util; static private variable

      setup: -> # called by Model ctor
        #@refreshPatches = false # for static patches
        
        # globals: UI generally manages these
        @population = 100
        @size = 1.5   # size in patch coords
        @speed = .5   # move forward this amount in patch coords
        @wiggle = u.degToRad(30) # degrees/radians to wiggle
        @startCircle = true  # initialize agents randomly or in circle
        
        for a in @agents.create @population
          a.size = @size
          a.shape = u.oneOf ABM.shapes.names() # random shapes
          if @startCircle
            a.forward @patches.maxX/2 # start in circle
          else
            a.setXY @patches.randomPt()... # set random location

        for p in @patches
          p.color = u.randomGray()
          
        # Print number of agents with each shape:
        for s in ABM.shapes.names()
          num = @agents.getWithProp("shape", s).length; console.log "#{num} #{s}"

      step: ->
        @updateAgents(a) for a in @agents
        @updatePatches(p) for p in @patches
        @reportInfo() if @ticks % 100 is 0

      updateAgents: (a) -> # a is agent
        a.heading += u.randomCentered @wiggle
        a.forward @speed
      updatePatches: (p) -> # p is patch
        # change color every 10 ticks
        p.color = u.randomGray() if u.randomInt(10) is 0
      reportInfo: ->
        console.log "agents: #{@agents.length} patches: #{@patches.length}"

    # divName, patchSize, minX, maxX, minY, maxY, isTorus = true
    #   NL Defaults: 13, -16, 16, -16, 16, true
    # @APP is window.APP, @ = outer coffeescript wrapper context
    @APP=new MyModel "layers", 13, -16, 16, -16, 16
    </script>

  </head>
  <body onload="APP.start()">
    <canvas id="testCanvas" >Your browser does not support HTML5 Canvas.</canvas>
    <div id="layers"></div>
  </body>
</html>