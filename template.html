<html>
  <head>
    <title>AgentScript Model</title>
    <script src="agentscript.js"></script>
    <script src="coffee-script.js"></script>
    <script type="text/coffeescript">
    # This is a general template for creating models.
    #
    # To build your own model, extend class ABM.Model supplying the 
    # two abstract methods `setup` and `step`.  `@foo` signifies
    # an instance variable or method.
    # See [CoffeeScript docs](http://jashkenas.github.com/coffee-script/#classes)
    # for explanation of context of a class and its constructor.
    #
    # We do not provide a constructor of our own. 
    # CoffeeScript automatically calls `Model.constructor <args>`
    # and `setup` will be called by `Model.constructor`. See:
    #
    #     APP=new MyModel "layers", 13, -16, 16, -16, 16
    #
    # below, which passes all its arguments to `Model`
    class MyModel extends ABM.Model
      # Static private variables, useful for "aliases" w/o adding to global namespace
      u = ABM.util
      log = (arg) -> console.log arg

      # Initialize our model via the `setup` abstract method.
      # This model simply creates `population` agents with
      # arbitrary shapes with `size` size and `speed` velocity.
      # We also periodically change the patch colors to random gray values.
      setup: -> # called by Model.constructor        
      # First, we initialize our model instance variables.
      # Most instance variables are parameters we would like
      # an external UI to setup for us.
        @population = 100
        @size = 1.5   # size in patch coords
        @speed = .5   # move forward this amount in patch coords
        @wiggle = u.degToRad(30) # degrees/radians to wiggle
        @startCircle = true  # initialize agents randomly or in circle
        @debug = true # turn on/off use of setRootVars() below
        
        # The patch grid will have been setup for us.  Here we initialize
        # patch variables, either built-in ones or any new patch variables
        # our model needs. In this case, we set the built-in color to a
        # random gray value.
        for p in @patches
          p.color = u.randomGray()

        # Our empty @agents AgentSet will have been created.  Here we
        # add `population` Agents we use in our model.
        # We set the build-in Agent variables `size` and `shape` 
        # and layout the agents randomly or in a circle depending
        # on our modeel's `startCircle` variable.
        for a in @agents.create @population
          a.size = @size
          a.shape = u.oneOf ABM.shapes.names() # random shapes
          if @startCircle
            a.forward @patches.maxX/2 # start in circle
          else
            a.setXY @patches.randomPt()... # set random location

        # Print number of agents and patches to the console.
        # Note CoffeeScript
        # [string interpolation](http://jashkenas.github.com/coffee-script/#strings)
        log "total agents: #{@agents.length}, total patches: #{@patches.length}"
        # Print number of agents with each shape
        for s in ABM.shapes.names()
          num = @agents.getWithProp("shape", s).length
          log "#{num} #{s}"

      # Update our model via the second abstract method, `step`
      step: ->  # called by Model.animate
        # First, update our agents via our `updateAgents` method
        @updateAgents(a) for a in @agents
        # Every 100 steps, update our patches, print stats to 
        # the console, and use the Model refresh flag to redraw
        # the patches. Otherwise don't refresh.
        if @ticks % 100 is 0
          @updatePatches(p) for p in @patches
          @reportInfo() 
          @refreshPatches = true
        else
          @refreshPatches = false
        # Stop the animation at 1000. Restart by `APP.start()` in console.
        if @ticks is 1000
          log "..stopping, restart by APP.start()"
          @stop()

      # Three of our own methods to manage agents & patches
      # and report model state.
      updateAgents: (a) -> # a is agent
        # Have our agent "wiggle" by changing
        # our heading by +/- `wiggle/2` radians
        a.rotate u.randomCentered @wiggle
        # Then move forward by our speed.
        a.forward @speed
      updatePatches: (p) -> # p is patch
        # Update patch colors to be a random gray.
        p.color = u.randomGray()
      reportInfo: ->
        # Report the average heading, in radians and degrees
        headings = @agents.getProp "heading"
        avgHeading = (headings.reduce (a,b)->a+b) / @agents.length
        # Note: multiline strings. block strings also available.
        log "
average heading of agents: 
#{avgHeading.toFixed(2)} radians, 
#{u.radToDeg(avgHeading).toFixed(2)} degrees"

    # Now that we've build our class, we call it with Model's
    # constructor arguments:
    #
    #     divName, patchSize, minX, maxX, minY, maxY,
    #     isTorus = true, topLeft=[10,10]
    #
    # Note: Netlogo defaults 13, -16, 16, -16, 16 <br>
    # for patchSize, minX, maxX, minY, maxY
    #
    # We do not strictly need to set the new model to a local variable,
    # class Model sets it to `ABM.model`. We do so here for convenience.
    APP = new MyModel "layers", 13, -16, 16, -16, 16
    # Finally set some debugging aids in the global namespace.
    # Here @APP is window.APP, see CoffeeScript
    # [lexical scoping](http://jashkenas.github.com/coffee-script/#lexical_scope)
    # for name scope discussion.
    (@APP = APP; APP.setRootVars()) if APP.debug
    </script>
  </head>
  <body onload="ABM.model.start()">
    <canvas id="testCanvas">Your browser does not support HTML5 Canvas.</canvas>
    <div id="layers"></div>
  </body>
</html>