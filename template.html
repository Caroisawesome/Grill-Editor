<html>
  <head>
    <title>CoffeeScript & Canvas ABM Test</title>
    <script src="agentscript.js"></script>
    <script src="coffee-script.js"></script>
    <script type="text/coffeescript">
    class MyModel extends ABM.Model
      # static private variables, useful for "aliases" w/o adding to global namespace
      u = ABM.util
      log = (arg) -> console.log arg

      setup: -> # called by Model ctor        
        # globals: UI generally manages these
        @population = 100
        @size = 1.5   # size in patch coords
        @speed = .5   # move forward this amount in patch coords
        @wiggle = u.degToRad(30) # degrees/radians to wiggle
        @startCircle = true  # initialize agents randomly or in circle
        @debug = true # turn on/off use of setRootVars()
        
        for a in @agents.create @population
          a.size = @size
          a.shape = u.oneOf ABM.shapes.names() # random shapes
          if @startCircle
            a.forward @patches.maxX/2 # start in circle
          else
            a.setXY @patches.randomPt()... # set random location

        for p in @patches
          p.color = u.randomGray()
          
        # Print number of agents with each shape:
        log "total agents: #{@agents.length}, total patches: #{@patches.length}"
        for s in ABM.shapes.names()
          num = @agents.getWithProp("shape", s).length
          log "#{num} #{s}"

      step: ->
        @updateAgents(a) for a in @agents
        if @ticks % 100 is 0
          @updatePatches(p) for p in @patches
          @reportInfo() 
          @refreshPatches = true
        else
          @refreshPatches = false

      updateAgents: (a) -> # a is agent
        a.rotate u.randomCentered @wiggle
        a.forward @speed
      updatePatches: (p) -> # p is patch
        p.color = u.randomGray()
      reportInfo: ->
        headings = @agents.getProp "heading"
        avgHeading = (headings.reduce (a,b)->a+b) / @agents.length
        # multiline strings. block strings also available.
        log "
average heading of agents: 
#{avgHeading.toFixed(2)} radians, 
#{u.radToDeg(avgHeading).toFixed(2)} degrees"

    # divName, patchSize, minX, maxX, minY, maxY, isTorus = true
    #   NL Defaults: 13, -16, 16, -16, 16, true
    # @APP is window.APP, @ = outer coffeescript wrapper context
    @APP=new MyModel "layers", 13, -16, 16, -16, 16
    APP.setRootVars() if @debug # add debugging aids to global scope
    </script>
  </head>
  <body onload="APP.start()">
    <canvas id="testCanvas">Your browser does not support HTML5 Canvas.</canvas>
    <div id="layers"></div>
  </body>
</html>