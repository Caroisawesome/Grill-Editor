<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">
    
    class MyModel extends ABM.Model
      u = ABM.util # utilities; static variable
      setup: -> # called by Model ctor
        @agentBreeds "nodes travelers"
        
        # no optimizations: 44fps
        @refreshPatches = false # for static patches
        @refreshAgents = false # for static agents
        # setFastPatches, setCacheAgentsHere, setAgentStaticColors would not apply
        # both: 57+fps (max)

        # globals
        @nodeCount = 50
        @travelersCount = 100
        @growPopulation = true
        @useInversion = true
        @bestTourNodes = []
        @bestTourLength = 0
        @bestTourTick = 0
        @stopTickDifference = 500
        
        # defaults
        @patches.setDefaultColor [250, 250, 0]
        @agents.setDefaultShape "circle"
        @agents.setDefaultColor [255,0,0]
        @agents.setDefaultHeading 0 # override promotion to random angle
        @links.setDefaultColor [255,0,0]

        # p.color = [250, 250, 0] for p in @patches
        
        @setupNode a for a in @agents.create @nodeCount
        
        @createTourLinks @nodes()
        @bestTourLength = @links.reduce ((sum,l)->sum+l.length()), 0 
        
        @agents.create @travelersCount, (a) => @setupTraveler(a)

      setupNode: (a) ->
        # a.color = [255,0,0]
        # a.shape = "circle"
        a.breed = "nodes"
        a.setXY @patches.randomPt()...
        a.setXY @patches.randomPt()... while @nodes().inRadius(a, 2).any()
      setupTraveler: (a) ->
        a.hidden = true
        a.breed = "travelers"
        a.tourNodes = @nodes().clone().shuffle()
        a.tourLength = @lengthFromNodes a.tourNodes
        
      step: ->
        @makeTour a for a in @travelers()
        @installBestTour()
        if (@ticks - @bestTourTick) is @stopTickDifference
          console.log "Stopping at tick #{@ticks} after no change in #{@stopTickDifference} ticks"
          console.log "Best tour: #{@bestTourLength} at tick #{@bestTourTick}"
          @stop()
      
      createTourLinks: (nodeList) ->
        @links.clear()
        @links.create nodeList[0], nodeList.last()
        @links.create nodeList[i], nodeList[i-1] for i in [1...nodeList.length]
        # @links.setProp "color", [255,0,0]
      lengthFromNodes: (nodeList) ->
        len = nodeList[0].distance(nodeList.last())
        len += nodeList[i].distance(nodeList[i-1]) for i in [1...nodeList.length]
        len
      installBestTour: ->
        @travelers().maxOneOf("tourLength").die() while @travelers().length>@travelersCount
        a = @travelers().minOneOf("tourLength")
        if a.tourLength < @bestTourLength
          @reportNewTour a
          @bestTourLength = a.tourLength
          @bestTourNodes = a.tourNodes
          @bestTourTick = @ticks
          @createTourLinks @bestTourNodes

      makeTour: (a) ->
        nlist = if @useInversion then @inversionStrategy a else @randomStrategy a
        len = @lengthFromNodes nlist
        if @growPopulation
        then a.hatch 1, (a) => a.tourNodes=nlist; a.tourLength=len
        else (a.tourNodes=nlist; a.tourLength=len) if len < a.tourLength
      randomStrategy: (a) -> a.tourNodes.clone().shuffle()
      inversionStrategy: (a) -> @asSet @newInversion a.tourNodes

      newInversion: (nlist) ->
        len = nlist.length
        i = u.randomInt len-1
        len = 2 + u.randomInt len-i-2
        [].concat (nlist.slice 0, i),
                  (nlist.slice i, i+len).reverse(),
                  (nlist.slice i+len)

      reportNewTour: (a) ->
        console.log "new best tour at tick #{@ticks}: #{a.tourLength} by traveler #{a.id}"
        
    # div, patchSize, minX, maxX, minY, maxY, isTorus=true, neighbors=true
    #   NL Defaults: 13, -16, 16, -16, 16
    APP=new MyModel "layers", 16, -14, 14, -14, 14, false, false
    APP.setRootVars()
    
    </script>

  </head>
  <body onload="ABM.model.start()">
    <canvas id="testCanvas" style="height:0;width:0;">Your browser does not support HTML5 Canvas.</canvas>
    <div id="layers"></div>
  </body>
</html>