<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../lib/data.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util#; DataSet = ABM.DataSet # aliases

    class MyModel extends ABM.Model
      startup: -> # called by constructor
        xhrText = u.xhrLoadFile "data/gisElevation.asc", "text", (response) =>
          console.log "xhrText onload started, parsing .asc file"
          @elevation = ABM.DataSet.ascDataSet response

      setup: ->
        @refreshPatches = @refreshLinks = false
        
        @vision = 3
        @speed = .25
        
        @agents.setDefaultShape "square"
        @agents.setDefaultSize 2
        @agents.setDefaultColor [100,100,150]
        @patches.cacheRect @vision, false # cache inRadius @vision
                
        console.log "..showing elevation in drawing layer"
        @patches.installDrawing @elevation.toImage(true)
        console.log "..setting patch elevation values via sampling .asc dataset"
        @elevation.setPatchVar("elevation")

        p.sprout 1 for p in @patches.nOf(@patches.length/10)
        console.log "patches: #{@patches.length}, agents: #{@agents.length}"      
        
      step: -> # stop: just one tick
        console.log @anim.toString() if @anim.ticks % 100 is 0
        moved = 0
        for a in @agents
          n = a.p.pRect.minOneOf "elevation" # cached vision rect
          if a.p.elevation > n.elevation
            a.face n
            a.forward @speed
            moved++
        if moved is 0
          console.log "done, ticks: #{@anim.ticks}"
          @stop()
        null
    
    # div, patchSize, minX, maxX, minY, maxY, isTorus=true, hasNeighbors=true
    #   NL Defaults: 13, -16, 16, -16, 16
    # APP = new MyModel "layers", 3, 0,160, 0,160, false
    APP = new MyModel "layers", 2, 0,319, 0,239, false
    APP.setRootVars() # Debug: Put Model vars in global name space
    APP.setupAndGo() # Run model immediately after startup initialization
    
    </script>
  </head>
  <body>
    <div id="layers" style="position:relative;padding:20;"></div>
  </body>
</html>