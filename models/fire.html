<html>
  <head>
    <title>AgentScript Model</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">
    class MyModel extends ABM.Model
      u = ABM.util # static variable
      
      setup: -> # called by Model ctor
        @agents.setDefaultShape "square"
        @agentBreeds "embers fires"
        
        @refreshPatches = false # <1fps->50-55fps, no surprise, app optimized for this 
        # No optimizations: 50-55fps
        # @setFastPatches() # 50-55fps .. not used, refresh off
        # @setCacheAgentsHere() # 50-55fps, not needed * overhead
        # @setAgentStaticColors() # fps, not large part of performance
        # None of the optimizations particularly useful, other than refresh
        # If refresh on, fastPatches is in the 50+fps range.
        
        @density = 60 # percent
        @burnedTrees = 0
        @initialTrees = 0
        
        # defaults
        @agents.setDefaultHeading(0) # override promotion to random angle

        for p in @patches when u.randomInt(100) < @density
          p.color = [0, 255, 0]
        @ignite p for p in @patches when p.x is @patches.minX
        @initialTrees = (p for p in @patches when u.colorsEqual p.color, [0, 255, 0]).length
        #console.log "burnedTrees #{@burnedTrees}"
        @burnedTrees = 0 # reset from initial ignites
      ignite: (p) ->
        p.sprout 1, (a) -> 
          a.breed = "fires"
          a.color = [255, 0, 0]
        p.color = [0, 0, 0] # in original model but apparently not needed, refresh off?
        @burnedTrees++
      fadeEmbers: ->
        for a in @embers()
          a.scaleColor a.color, .8
          if 100 > Math.max a.color...
            a.p.color = a.color
            a.p.draw @contexts.patches
            a.die()
      step: ->
        if not @agents.any() then @stop()
        for a in @fires()
          @ignite p for p in a.p.n4 when u.colorsEqual p.color, [0, 255, 0]
          a.breed = "embers"
        @fadeEmbers()

    # div, patchSize, minX, maxX, minY, maxY, isTorus=true, neighbors=true
    #   NL Defaults: 13, -16, 16, -16, 16
    APP=new MyModel "layers", 2, -125, 125, -125, 125, false #not torus
    </script>

  </head>
  <body onload="ABM.model.start()">
    <canvas id="test" style="height:0;width:0">Browser doesn't support HTML5 Canvas</canvas>
    <div id="layers" style="position:relative;padding:20;"></div>
  </body>
</html>