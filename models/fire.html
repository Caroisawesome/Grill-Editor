<html>
  <head>
    <title>CoffeeScript & Canvas ABM Test</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">
    class MyModel extends ABM.Model
      u = ABM.util # static variable
      setup: -> # called by Model ctor
        @refreshPatches = false
        @agents.setDefaultShape "square"
        @agentBreeds "embers fires"
        
        @density = 60 # percent
        @burnedTrees = 0
        @initialTrees = 0
        
        for p in @patches when u.randomInt(100) < @density
          p.color = [0, 255, 0]
        @ignite p for p in @patches when p.x is @patches.minX
        @initialTrees = (p for p in @patches when u.colorsEqual p.color, [0, 255, 0]).length
        #console.log "burnedTrees #{@burnedTrees}"
        @burnedTrees = 0 # reset from initial ignites
      ignite: (p) ->
        p.sprout 1, (a) -> 
          a.breed = "fires"
          a.color = [255, 0, 0]
        p.color = [0, 0, 0]
        @burnedTrees++
      fadeEmbers: ->
        for a in @embers()
          a.color = u.scaleColor a.color, .8
          if 100 > Math.max a.color...
            a.p.color = a.color
            a.p.draw @layers[0] # REMIND: @ctxFor @patches
            a.die()
      step: ->
        if not @agents.any() then @stop()
        for a in @fires()
          @ignite p for p in a.p.n4 when u.colorsEqual p.color, [0, 255, 0]
          a.breed = "embers"
        @fadeEmbers()

    # divName, patchSize, minX, maxX, minY, maxY, isTorus = true
    #   NL Defaults: 13, -16, 16, -16, 16, true
    # @APP is window.APP, @ = outer coffeescript wrapper context
    @APP=new MyModel "layers", 2, -125, 125, -125, 125, false #not torus
    </script>

  </head>
  <body onload="APP.start()">
    <canvas id="canvas" >Your browser does not support HTML5 Canvas.</canvas>
    <div id="layers"></div>
  </body>
</html>