<html>
  <head>
    <title>CoffeeScript & Canvas ABM Test</title>
    <script src="../agentscript.js"></script>
    <script src="../coffee-script.js"></script>
    <script type="text/coffeescript">
    class MyModel extends ABM.Model
      u = ABM.util # utilities; static variable
      setup: -> # called by Model ctor
        @refreshPatches = false # for static patches
        @refreshLinks = false # for static links
        @agentBreeds "nodes drivers"
        
        # globals
        @numNodes = 30
        @numDrivers = 100
        @layoutCircle = true
        @baseVelocity = 0.1 # patchs
        @velocityDelta = 0.1 # patches
        
        p.color = u.randomGray(10,50) for p in @patches
        for p in @patches.nOf @numNodes
          p.sprout 1, (a) => 
            a.breed = "nodes"
            a.shape = "circle"; a.size = .3 # dot == small circle
            if @nodes().length > 1
              @links.create a, @nodes().other(a).oneOf()
        
        if @layoutCircle then @links.layoutCircle @nodes(), @patches.maxX - 1
        
        for i in [1..@numDrivers]
          n = @nodes().oneOf()
          n.hatch 1, (a) =>
            a.breed = "drivers"
            a.fromNode = a.toNode = n.linkNeighbors().oneOf()
            a.face a.toNode
            a.size = 1.5
            a.v = @baseVelocity + u.randomFloat @velocityDelta
            
      step: ->
        for d in @drivers()
          d.face d.toNode
          d.forward Math.min d.v, d.distance d.toNode
          if .01 > d.distance d.toNode # or (d.distance d.toNode) < .01
            d.fromNode = d.toNode
            d.toNode = d.toNode.linkNeighbors().oneOf()

    # divName, patchSize, minX, maxX, minY, maxY, isTorus = true
    #   NL Defaults: 13, -16, 16, -16, 16, true
    # @APP is window.APP, @ = outer coffeescript wrapper context
    @APP=new MyModel "layers", 13, -16, 16, -16, 16, false
    APP.setRootVars() # debug
    </script>

  </head>
  <body onload="APP.start()">
    <canvas id="testCanvas">Your browser does not support HTML5 Canvas.</canvas>
    <div id="layers"></div>
  </body>
</html>